# vim: ft=python expandtab
Import('env')

env_gobject = env.Clone()
env_gobject['PDB'] = 'libgobject.pdb'

env_gobject['CPPDEFINES']=['HAVE_CONFIG_H',
                                ('G_LOG_DOMAN', '\\\"GLib-GObject\\\"'),
                                'G_DISABLE_DEPRECATED',
                                'GOBJECT_COMPILATION',
                                'G_DISABLE_CONST_RETURNS'] + env_gobject['DEBUG_CPPDEFINES']
env_gobject.Append(CPPPATH=['#', '#glib'])
env_gobject.Append(CFLAGS=env_gobject['DEBUG_CFLAGS'])

#glib-mkenums
env_gobject['DOT_IN_SUBS']['@PERL_PATH@'] = env_gobject['PERL']
glib_mkenums = env_gobject.DotIn('glib-mkenums', 'glib-mkenums.in')
env_gobject.Depends(glib_mkenums, 'SConscript')

Alias('install', env_gobject.Install('$PREFIX/bin', 'glib-mkenums'))

#glib-genmarshal
env_gobject_genmarshal = env_gobject.Clone(PDB = 'glib-genmarshal.pdb')
Alias('install', env_gobject.Install('$PREFIX/bin', 'glib-genmarshal.exe'))

def dot_strings(target, source, env):
    t = file(str(target[0]), 'w')
    s = file(str(source[0]), 'r')
    for line in s.readlines():
        '''
	grep '^[A-Z]' $(srcdir)/gmarshal.list \
	| sed -e 's/^/"g_cclosure_marshal_/' -e 's/:/__/' -e 's/,/_/g' -e 's/$$/",/' > xgen-gms \
        '''
        if line[0].isupper():
            line = '"g_cclosure_marshal_' + line
            line = line.replace(':', '__')
            line = line.replace(',', '_')
            line = line.replace('\n', '",\n')
            t.write(line)
    s.close()
    t.close()

dot_strings_processor = env_gobject_genmarshal.Builder(action= dot_strings, src_suffix = '.list')
env_gobject_genmarshal.Append(BUILDERS={'DotStrings': dot_strings_processor})

env_gobject_genmarshal.DotStrings('gmarshal.strings', 'gmarshal.list')
genmarshal = env_gobject_genmarshal.Program('glib-genmarshal.c', LIBS='libglib-2.0', LIBPATH='#glib')
env_gobject_genmarshal.Depends(genmarshal, 'gmarshal.strings')
env_gobject_genmarshal.Depends(genmarshal, '#glib/libglib-2.0.lib')
env_gobject_genmarshal.AddPostAction(genmarshal, 'mt.exe -nologo -manifest ${TARGET}.manifest -outputresource:$TARGET;2')

#gobject
env_gobject['DOT_SYMBOLS_FLAGS'] = '/DINCLUDE_VARIABLES /DINCLUDE_INTERNAL_SYMBOLS /DG_OS_WIN32 /DALL_FILES'
env_gobject.DotSymbols2Def('gobject.def', 'gobject.symbols')

env_gobject.Command('gobjectalias.h', 'gobject.symbols', env_gobject['PERL'] + ' gobject/makegobjectalias.pl <$SOURCE >$TARGET')
env_gobject.Command('gobjectaliasdef.c', 'gobject.symbols', env_gobject['PERL'] + ' gobject/makegobjectalias.pl -def <$SOURCE >$TARGET')

gobject_HEADERS = Split("\
	gboxed.h		\
	gclosure.h		\
	genums.h		\
	gobject.h		\
	gparam.h		\
	gparamspecs.h		\
	gsignal.h		\
	gsourceclosure.h	\
	gtype.h			\
	gtypemodule.h		\
	gtypeplugin.h		\
	gvalue.h		\
	gvaluearray.h		\
	gvaluecollector.h	\
	gvaluetypes.h		\
	gmarshal.h")

gobject_SOURCES = Split("\
	gboxed.c		\
	gclosure.c		\
	genums.c		\
	gobject.c		\
	gparam.c		\
	gparamspecs.c		\
	gsignal.c		\
	gsourceclosure.c	\
	gtype.c			\
	gtypemodule.c		\
	gtypeplugin.c		\
	gvalue.c		\
	gvaluearray.c		\
	gvaluetransform.c	\
	gvaluetypes.c")
BUILT_gobject_SOURCES = Split('gobjectaliasdef.c')

env_gobject.AppendENVPath('PATH', ['#', '#glib', '#win32/libintl-proxy']) 
env_gobject.Command('gmarshal.c', 'gmarshal.list', 
        '"gobject\\glib-genmarshal" --nostdinc --prefix=g_cclosure_marshal $SOURCE --body >> $TARGET')
env_gobject.Depends('gmarshal.c', genmarshal)

env_gobject.Command('gmarshal.h', 'gmarshal.list', '''
	echo #ifndef __G_MARSHAL_H__ > $TARGET
	echo #define __G_MARSHAL_H__ >> $TARGET
        "gobject\\glib-genmarshal.exe" --nostdinc --prefix=g_cclosure_marshal $SOURCE --header >> $TARGET
	echo #endif /* __G_MARSHAL_H__ */ >> $TARGET''')
env_gobject.Depends('gmarshal.h', genmarshal)

env_gobject.DotIn('gobject.rc', 'gobject.rc.in')
env_gobject.RES('gobject.rc')
gobject_dll=env_gobject.SharedLibrary(['libgobject-2.0' + env_gobject['LIB_SUFFIX'] + env_gobject['SHLIBSUFFIX'],
                                            'libgobject-2.0.lib'],
                                           gobject_SOURCES + BUILT_gobject_SOURCES + ['gobject.def', 'gobject.res'], 
                                           LIBS=['libintl-proxy',
                                                 'libglib-2.0',
                                                 'user32', 
                                                 'advapi32', 
                                                 'shell32', 
                                                 'ole32'],
                                           LIBPATH=['$PREFIX/lib', '#glib', '#win32/libintl-proxy'])
env_gobject.Depends('gsignal.obj', 'gmarshal.c') #gsignal.c include this file, itself is not listed on source files though
env_gobject.Depends(gobject_dll, gobject_HEADERS)
env_gobject.Depends(gobject_dll, genmarshal)

env_gobject.AddPostAction(gobject_dll, 'mt.exe -nologo -manifest ${TARGET}.manifest -outputresource:$TARGET;2')

subdirs = []
if ARGUMENTS.get('build_test', 0):
    subdirs += ['tests/SConscript']
SConscript(subdirs, exports='env_gobject')

Alias('insall', env_gobject.Install('$PREFIX/bin', 'glib-genmarshal.exe'))

Alias('install', env_gobject.Install('$PREFIX/include/glib-2.0/gobject', gobject_HEADERS))
Alias('install', env_gobject.Install('$PREFIX/bin', 'libgobject-2.0' + env_gobject['LIB_SUFFIX'] + '.dll'))
Alias('install', env_gobject.Install('$PREFIX/lib', 'libgobject-2.0.lib'))
Alias('install', env_gobject.InstallAs('$PREFIX/lib/gobject-2.0.lib', 'libgobject-2.0.lib'))

if env['DEBUG'] == 1:
    Alias('install', env_gobject.Install('$PREFIX/pdb', 'libgobject.pdb'))
