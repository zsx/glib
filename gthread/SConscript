# vim: ft=python expandtab
from site_init import *
Import('env')

env_gthread = env.Clone(PDB = 'libgthread-2.0.pdb', PARENT_ENV = env)

env_gthread.Append(CPPDEFINES=['HAVE_CONFIG_H',
                                ('G_LOG_DOMAN', r'\"GThread\"'),
                                ('G_THREAD_SOURCE', r'\"gthread-win32.c\"'),
                                'G_DISABLE_DEPRECATED'] + env_gthread['DEBUG_CPPDEFINES'])
env_gthread.Append(CPPPATH=['#', '#glib', '#gthread'])

gthread_SOURCES = Split("\
	gthread-impl.c")

env_gthread.Depends('gthread-impl' + env_gthread['OBJSUFFIX'], 'gthread-win32.c') 
env_gthread.DotIn('gthread.rc', 'gthread.rc.in')
env_gthread.RES('gthread.rc')
env_gthread.Append(LIBS=['libglib-2.0', 'user32'],
                   LIBPATH=['$PREFIX/lib', '#glib'])
gthread_dll=env_gthread.SharedLibrary(['libgthread-2.0' + env_gthread['LIB_SUFFIX'] + env_gthread['SHLIBSUFFIX'],
                                    'libgthread-2.0.lib'],
                                   gthread_SOURCES + ['gthread.def', 'gthread.res'])

env_gthread.AddPostAction(gthread_dll, 'mt.exe -nologo -manifest ${TARGET}.manifest -outputresource:$TARGET;2')

env_gthread['DOT_IN_SUBS']['@VERSION@'] = env['PACKAGE_VERSION']

InstallRun('$PREFIX/bin', 'libgthread-2.0' + env_gthread['LIB_SUFFIX'] + '.dll', env_gthread)
env_gthread['DOT_IN_SUBS']['@DLLS@'] = generate_file_element('libgthread-2.0' + env_gthread['LIB_SUFFIX'] + '.dll', 'bin', env_gthread)

InstallDev('$PREFIX/lib', 'libgthread-2.0.lib', env_gthread)
InstallDevAs('$PREFIX/lib/gthread-2.0.lib', 'libgthread-2.0.lib', env_gthread)
env_gthread['DOT_IN_SUBS']['@LIBS@'] = generate_file_element(['libgthread-2.0.lib', 'gthread-2.0.lib'], 'lib', env_gthread)

if env_gthread['DEBUG']:
    InstallDev('$PREFIX/pdb', 'libgthread-2.0.pdb', env_gthread)
env_gthread['DOT_IN_SUBS']['@PDBS@'] = generate_file_element(env_gthread['PDB'], 'pdb', env_gthread)
env_gthread['DOT_IN_SUBS']['@PCS@'] = generate_file_element('gthread-2.0.pc', 'lib/pkgconfig', env_gthread)

env_gthread.DotIn('gthreadrun.wxs', 'gthreadrun.wxs.in')
env_gthread.DotIn('gthreaddev.wxs', 'gthreaddev.wxs.in')
env_gthread.Depends(['gthreadrun.wxs', 'gthreaddev.wxs'], 'SConscript')
InstallDev('$PREFIX/wxs', 'gthreadrun.wxs', env_gthread)
InstallDev('$PREFIX/wxs', 'gthreaddev.wxs', env_gthread)
#DumpInstalledFiles(env_gthread)
if ARGUMENTS.get('build_test', 0):
    SConscript('tests/SConscript', exports = 'env_gthread')
