# vim: ft=python expandtab
Import('env_glib prefix')

env_glib_gthread = env_glib.Clone(PDB = 'libglib-2.0.pdb')

env_glib_gthread['CPPDEFINES']=['HAVE_CONFIG_H',
                                ('G_LOG_DOMAN', r'\"GThread\"'),
                                ('G_THREAD_SOURCE', r'\"gthread-win32.c\"'),
                                'G_DISABLE_DEPRECATED'] + env_glib_gthread['DEBUG_CPPDEFINES']
env_glib_gthread.Append(CPPPATH=['#glib', '#glib/glib', '#glib/gthread'])

gthread_SOURCES = Split("\
	gthread-impl.c")

env_glib_gthread.Depends('gthread-impl' + env_glib_gthread['OBJSUFFIX'], 'gthread-win32.c') 
env_glib_gthread.DotIn('gthread.rc', 'gthread.rc.in')
env_glib_gthread.RES('gthread.rc')

gthread_dll=env_glib_gthread.SharedLibrary(['libgthread-2.0' + env_glib_gthread['LIB_SUFFIX'] + env_glib_gthread['SHLIBSUFFIX'],
                                    'libgthread-2.0.lib'],
                                   gthread_SOURCES + ['gthread.def', 'gthread.res'], 
                                   LIBS=['libglib-2.0', 'user32'],
                                   LIBPATH=[prefix + '/lib', '#glib/glib'])

env_glib_gthread.AddPostAction(gthread_dll, 'mt.exe -nologo -manifest ${TARGET}.manifest -outputresource:$TARGET;2')

Alias('install', env_glib_gthread.Install(prefix + '/bin', 'libgthread-2.0' + env_glib_gthread['LIB_SUFFIX'] + '.dll'))
Alias('install', env_glib_gthread.Install(prefix + '/lib', 'libgthread-2.0.lib'))
Alias('install', env_glib_gthread.Install(prefix + '/pdb', 'libgthread-2.0.pdb'))
